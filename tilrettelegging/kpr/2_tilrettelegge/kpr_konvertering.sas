%macro kpr_konvertering(inndata= , utdata=);

data &utdata;
set &inndata;

%if &diagnose eq 1 %then %do; /*kjøres kun på diagnose-fil*/
/*
- Rename 'diagnoseKode' til 'diag'. 
*/
rename diagnoseKode = diag_kpr;
/* 
- Lager 'erHdiag'. Sletter 'erHoveddiagnose'.
 */
erhdiag_kpr = erhoveddiagnose +0;
drop erhoveddiagnose;
%end;


%if &regning eq 1 %then %do; /*Koden kjøres kun på regningsfil*/
/*
- Lager 'pid_kpr' fra 'KPR_lnr' (løpenummer) og sletter 'KPR_lnr'
*/
pid_kpr=KPR_lnr+0;
Drop KPR_lnr;
/*
- Rename 'hoveddiagnoseKode' til 'Hdiag'. 
*/
rename hoveddiagnoseKode = hdiag_kpr;
/*
- Lager 'bydel_kpr' og sletter 'bydel2'. 
*/
bydel_kpr = bydel2 + 0;
drop bydel2;
/* 
- Lager 'kontakttype_kpr'. Endre kontakttype_kpr '-1' til '0'. Slette 'kontakttypenavn' og 'kuhrKontakttypeId'.
 */
kontakttype_kpr = kuhrKontakttypeId +0;
if kontakttype_kpr eq -1 then kontakttype_kpr = 0;
drop kontakttypenavn kuhrKontakttypeId;
/*
- Lager 'refusjonutbetalt' fra 'refusjonutbetaltbeløp'. 
*/
refusjonutbetalt = 'refusjonutbetaltbeløp'n + 0;
drop 'refusjonutbetaltbeløp'n;
%end;

run;

%mend;